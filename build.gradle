apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.junit.platform.gradle.plugin'

group = 'com.github.kazuhito_m'
version = '1'

description = 'executable-war-sample'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

war {
    from "$buildDir/classes/main"
    from "$buildDir/resources/main"
    from "$buildDir/unpack-dependencies"
    from {
        configurations.compile.collect {
            zipTree(it)
        }
    }
    classpath = classpath.filter {
        !it.name.startsWith('tomcat-embed-') && !it.name.startsWith('ecj-')
    }
    manifest {
        attributes(
                "Main-Class": "com.github.kazuhito_m.TomcatLauncher",
        )
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}


dependencies {
    def tomcatVersion = '7.0.47' // 7.0.47 以降は「ExpandWar.java のWar展開でエラーが出る」が、起動自体は成功する。
    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.2'
}
